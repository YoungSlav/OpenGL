#version 430

layout (local_size_x = 16, local_size_y = 16) in;

layout(rgba32f, binding = 0) uniform image2D outputTexture;

const float FLT_MAX = 3.402823466e+38;
const float PI = 3.14159265359;



struct Particle
{
    vec3 Position;
    vec3 PredictedPosition;
    vec3 Velocity;
    float Density;
    float iDensity;
};
struct SortedParticle
{
    uint CellHash;
    uint CellIndex;
    uint pIndex;
};

// BUFFERS
layout(std430, binding = 0) buffer ParticlesBuffer
{
	Particle Particles[];
};
layout(std430, binding = 1) buffer SortedParticlesBuffer
{
	SortedParticle SortedParticles[];
};
layout(std430, binding = 2) buffer StartIndicesBuffer
{
	uint StartIndices[];
};

uniform vec3 eyeLocation;
uniform mat4 invProjMat;
uniform mat4 invViewMat;

uniform vec3 WorldSize;
uniform mat4 BoundingBox;
uniform mat4 inverseBoundingBox;

uniform uint ParticleCount;
uniform float DensityScale;
uniform float ParticleMass;
uniform float SmoothingRadius;
uniform float SmoothingRadius2;
uniform float TargetDensity;

uniform float MarchingRayStep;
uniform float DensityMultiplier;

uniform vec3 ScatteringCoefficients;

const uint GridLookupCount = 27;
const ivec3 GridOffsets[GridLookupCount] = ivec3[](
    ivec3(0, 0, 0),
    ivec3(1, 0, 0),
    ivec3(-1, 0, 0),
    ivec3(0, 1, 0),
    ivec3(0, -1, 0),
    ivec3(0, 0, 1),
    ivec3(0, 0, -1),
    ivec3(1, 1, 0),
    ivec3(1, -1, 0),
    ivec3(-1, 1, 0),
    ivec3(-1, -1, 0),
    ivec3(1, 0, 1),
    ivec3(1, 0, -1),
    ivec3(-1, 0, 1),
    ivec3(-1, 0, -1),
    ivec3(0, 1, 1),
    ivec3(0, 1, -1),
    ivec3(0, -1, 1),
    ivec3(0, -1, -1),
    ivec3(1, 1, 1),
    ivec3(1, 1, -1),
    ivec3(1, -1, 1),
    ivec3(1, -1, -1),
    ivec3(-1, 1, 1),
    ivec3(-1, 1, -1),
    ivec3(-1, -1, 1),
    ivec3(-1, -1, -1)
);


float CalcDensity(vec3 location);

// kernels
float DensityKernel(float dst);

// grid helpers
ivec3 GetCellCoord(vec3 Position);
uint GetCellHash(ivec3 Coords);
uint CellIndexFromHash(uint Hash);


// marching
struct Ray
{
    vec3 origin;
    vec3 dir;
};


bool IntersectRayBox(Ray ray, out vec3 hitLocation, out float distInsideCube2);
vec3 RayMarch(vec3 dir, vec3 origin, float marchingDist2);


void main()
{
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 iSize = imageSize(outputTexture);
    if (pixelCoord.x >= iSize.x || pixelCoord.y >= iSize.y)
    {
        return;  // Ignore out-of-bounds invocations
    }

    // Convert pixel coordinates to NDC
    vec2 ndc = vec2(
        (2.0 * pixelCoord.x) / iSize.x - 1.0,
        (2.0 * pixelCoord.y) / iSize.y - 1.0
    );

    vec4 clipCoord = vec4(ndc, -1.0, 1.0);

    vec4 viewCoord = invProjMat * clipCoord;
    viewCoord /= viewCoord.w;

    vec3 worldCoord = (invViewMat * vec4(viewCoord.xyz, 1.0)).xyz;

    vec3 camRight = vec3(invViewMat[0][0], invViewMat[0][1], invViewMat[0][2]);
    vec3 camUp = vec3(invViewMat[1][0], invViewMat[1][1], invViewMat[1][2]);
        
    Ray ray;
    ray.origin = eyeLocation;
    ray.dir = normalize(worldCoord - eyeLocation);

    vec4 Color = vec4(0.0f);
    
    vec3 hitLocation;
    float marchingDistance2;
    if ( IntersectRayBox(ray, hitLocation, marchingDistance2) )
    {
        Color = vec4(RayMarch(ray.dir, hitLocation, marchingDistance2), 1.0f);
    }

    imageStore(outputTexture, pixelCoord, Color);
};

vec3 RayMarch(vec3 dir, vec3 origin, float marchingDist2)
{
    float totalDensity = 0.0f;
    vec3 totalLight = vec3(0.0f);

    const float jumpDist = MarchingRayStep * 5.0f;
    bool bJumping = false;

    float coveredDst = 0.0f;
    do
    {
        vec3 samplePoint = origin + dir * coveredDst;
        float density = CalcDensity(samplePoint) * MarchingRayStep * DensityMultiplier;

        totalDensity += density;

        vec3 inScatteredLight = vec3(1.0f) * totalDensity * ScatteringCoefficients;
        vec3 lightDecay = exp(-totalDensity * ScatteringCoefficients);
        totalLight += inScatteredLight * lightDecay;

        coveredDst += MarchingRayStep;
    } while( coveredDst*coveredDst < marchingDist2 );
    
    return totalLight;
}

float CalcDensity(vec3 location)
{
    float Density = 0.0f;
    ivec3 myCell = GetCellCoord(location);
    for ( uint gr = 0; gr < GridLookupCount; ++gr )
    {
        ivec3 lookupCell = myCell + GridOffsets[gr];
        uint CellHash = GetCellHash(lookupCell);
        uint CellIndex = CellIndexFromHash(CellHash);
        for ( uint lookupIndex = StartIndices[CellIndex]; lookupIndex < ParticleCount; ++lookupIndex )
        {
            if ( SortedParticles[lookupIndex].CellIndex != CellIndex )
                break;
            if ( SortedParticles[lookupIndex].CellHash != CellHash )
                continue;
            uint otherP = SortedParticles[lookupIndex].pIndex;

            // now we got all the indices
            vec3 otherPosition = Particles[otherP].Position;
            vec3 offset = location - otherPosition;
            float dst2 = dot(offset, offset);
            if ( dst2 > SmoothingRadius2 ) continue;
            
            // now we have all the data

            Density += ParticleMass * DensityKernel(dst2);
        }
    }
    return Density;
}

bool IntersectRayBox(Ray worldRay, out vec3 hitLocation, out float distInsideCube2)
{
    Ray ray;
    ray.origin = (inverseBoundingBox * vec4(worldRay.origin, 1.0)).xyz;
    ray.dir = normalize((inverseBoundingBox * vec4(worldRay.dir, 0.0)).xyz);

    // Box bounds
    vec3 minBound = vec3(-0.5);
    vec3 maxBound = vec3(0.5);

    // Calculate the t-values for intersection with each slab
    vec3 tMin = (minBound - ray.origin) / ray.dir;
    vec3 tMax = (maxBound - ray.origin) / ray.dir;

    // Ensure tMin < tMax
    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);

    // Find the largest entry t and the smallest exit t
    float tNear = max(max(t1.x, t1.y), t1.z);
    float tFar = min(min(t2.x, t2.y), t2.z);

    // If tNear > tFar or tFar < 0, there is no intersection
    if (tNear > tFar || tFar < 0.0)
    {
        return false;
    }

    // Populate the hit info
    hitLocation = ray.origin + ray.dir * tNear;
    vec3 hitFar = ray.origin + ray.dir * tFar;

    hitLocation = (BoundingBox * vec4(hitLocation, 0.0)).xyz;
    hitFar = (BoundingBox * vec4(hitFar, 0.0)).xyz;

    //float scaleToWorld = length((BoundingBox * vec4(ray.dir, 1)).xyz);
    //float nearDistance = scaleToWorld * tFar;
    //float farDistance = scaleToWorld * tFar;

    vec3 offset = (hitFar - hitLocation);

    distInsideCube2 = dot(offset, offset);

    return true;
}

// kernels
float DensityKernel(float dst2)
{
    float v = SmoothingRadius2 - dst2;
	return v * v * v * DensityScale;
}

// GRID

ivec3 GetCellCoord(vec3 Position)
{
	vec3 normilizedPos = Position + WorldSize;
    
    float scaleF = 1.0f / SmoothingRadius*2.0f;
    return ivec3(floor(normilizedPos * scaleF));
}

uint GetCellHash(ivec3 Coords)
{	
	const uint PRIME1 = 0x9e3779b1;
    const uint PRIME2 = 0x85ebca6b;
    const uint PRIME3 = 0xc2b2ae35;

	return Coords.y * PRIME1 + Coords.x * PRIME2 + Coords.z * PRIME3;
}

uint CellIndexFromHash(uint Hash)
{
    return Hash % ParticleCount;
}