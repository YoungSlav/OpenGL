#version 430

layout (local_size_x = 16, local_size_y = 16) in;

layout(rgba32f, binding = 0) uniform image2D outputTexture;
layout(r32f, binding = 1) uniform image3D densitiesMap;
uniform vec3 DensitiesMapSize;

const float FLT_EPS = 0.001f;
const float FLT_MAX = 3.402823466e+38;
const float PI = 3.14159265359;


uniform vec3 eyeLocation;
uniform mat4 invProjMat;
uniform mat4 invViewMat;

uniform float DensitiesResolutionInverse;
uniform vec3 BoundsSize;
uniform vec3 BoundsOrigin;
uniform mat4 BoundingBox;
uniform mat4 inverseBoundingBox;

uniform float MarchingRayStep;
uniform float LightMarchingRayStep;
uniform float DensityMultiplier;
uniform float DensityOffset;

uniform float TargetDensity;

uniform vec3 ScatteringCoefficients;


uniform vec3 DirToLight;

// debug
uniform float SurfaceStepsTreashold;






float SampleDensity(vec3 location);

// kernels
float DensityKernel(float dst);

// grid helpers
ivec3 GetCellCoord(vec3 Position);
uint GetCellHash(ivec3 Coords);
uint CellIndexFromHash(uint Hash);



bool IntersectRayBox(vec3 origin, vec3 dir, out vec3 hitLocation, out float distInsideCube2);
vec4 RayMarch(vec3 dir, vec3 origin, float marchingDist2);

float DensityAlongRay(vec3 origin, vec3 dir);
float FindSurface(vec3 origin, vec3 dir);


void main()
{
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 iSize = imageSize(outputTexture);
    if (pixelCoord.x >= iSize.x || pixelCoord.y >= iSize.y)
    {
        return;  // Ignore out-of-bounds invocations
    }

    // Convert pixel coordinates to NDC
    vec2 ndc = vec2(
        (2.0 * pixelCoord.x) / iSize.x - 1.0,
        (2.0 * pixelCoord.y) / iSize.y - 1.0
    );

    vec4 clipCoord = vec4(ndc, -1.0, 1.0);

    vec4 viewCoord = invProjMat * clipCoord;
    viewCoord /= viewCoord.w;

    vec3 worldCoord = (invViewMat * vec4(viewCoord.xyz, 1.0)).xyz;

    vec3 camRight = vec3(invViewMat[0][0], invViewMat[0][1], invViewMat[0][2]);
    vec3 camUp = vec3(invViewMat[1][0], invViewMat[1][1], invViewMat[1][2]);
        
    
    vec3 rayOrigin = eyeLocation;
    vec3 rayDir = normalize(worldCoord - eyeLocation);

    vec4 Color = vec4(0.0f);
    
    vec3 hitLocation;
    float marchingDistance2;
    if ( IntersectRayBox(rayOrigin, rayDir, hitLocation, marchingDistance2) )
    {
        vec4 totalLight = RayMarch(rayDir, hitLocation, marchingDistance2);
        
        
        Color = vec4(totalLight);
    }

    imageStore(outputTexture, pixelCoord, Color);
};

vec4 RayMarch(vec3 dir, vec3 origin, float marchingDist2)
{
    vec3 totalLight = vec3(0.0f);
    float accumulatedDensity = 0.0f;

    float Step = MarchingRayStep;

    float debugCount = 0.0f;
        
    for (float coveredDst = 0.0f; coveredDst*coveredDst < marchingDist2; coveredDst += Step)
    {
        vec3 samplePoint = origin + dir * coveredDst;
        float density = SampleDensity(samplePoint);
        float densityAlongStep = (density * Step * DensityMultiplier);
        accumulatedDensity += densityAlongStep;

        
        if ( density > DensityOffset )
        {
            float densityAlongSunRay = DensityAlongRay(samplePoint, DirToLight);
            vec3 transmittedLight = exp(-densityAlongSunRay * ScatteringCoefficients);
        
            vec3 scatteredLight = transmittedLight * densityAlongStep * ScatteringCoefficients;
            vec3 viewTransmittance = exp(-accumulatedDensity * ScatteringCoefficients);
        
            totalLight += scatteredLight * viewTransmittance;
        }


        // TODO: check if this optimization gives anything
        Step = density >= TargetDensity ? MarchingRayStep * (density / TargetDensity) : MarchingRayStep;
    }
    
    // actual
    return vec4(totalLight, 1.0f);
}



float DensityAlongRayDetailed_A(vec3 origin, vec3 dir)
{
    float totalDensity = 0.0f;
    float coveredDst = 0.0f;
    float pointDensity = 0.0f;
    do
    {
        vec3 samplePoint = origin + dir * coveredDst;
        pointDensity = SampleDensity(samplePoint) * DensityMultiplier;
        float densityAlongStep = (pointDensity * LightMarchingRayStep);
        
        totalDensity += densityAlongStep;
        coveredDst += LightMarchingRayStep;
    
    } while ( pointDensity > FLT_EPS );
    return totalDensity;
}

float DensityAlongRay_B(vec3 origin, vec3 dir)
{
    if ( SampleDensity(origin) <= FLT_EPS  )
        return 0.0f;

    float pointDensity = 0.0f;
    bool bFound = false;

    #define STEP_SIZE_NUMBER 5
    float Step[STEP_SIZE_NUMBER] = { LightMarchingRayStep*2.0f, LightMarchingRayStep, LightMarchingRayStep*0.5f, LightMarchingRayStep*0.25f, MarchingRayStep };
    uint StepSizeIndex = 0;
    uint StepIteration = 1;
    float DistCandidate = 0.0f;
    
    float LastDensityDelta = 0.0f;
    float totalDensity = 0.0f;

    while( true )
    {
        float stepDistance = (Step[StepSizeIndex] * StepIteration);
        float sampleDist = DistCandidate + stepDistance;
        vec3 samplePoint = origin + dir * sampleDist;
        pointDensity = SampleDensity(samplePoint);
        if ( pointDensity <= FLT_EPS )
        {
            DistCandidate = DistCandidate + (Step[StepSizeIndex] * (StepIteration-1));

            StepSizeIndex++;
            if( StepSizeIndex == STEP_SIZE_NUMBER )
            {
                break;
            }
            else
            {
                StepIteration = 1;  // skipping over 0 itteration of the next step size, not to sample same point again
            }
        }
        else
        {
            totalDensity += pointDensity * DensityMultiplier * stepDistance;
            StepIteration++;
        }

    }
    return totalDensity;
}


float DensityAlongRay(vec3 origin, vec3 dir)
{
    float surfaceDistance =  FindSurface(origin, dir);
    float totalDensity = TargetDensity * DensityMultiplier * surfaceDistance;
    return totalDensity;
}

float FindSurface(vec3 origin, vec3 dir)
{

    if ( SampleDensity(origin) <= FLT_EPS  )
        return 0.0f;

    float pointDensity = 0.0f;
    bool bFound = false;

    #define STEP_SIZE_NUMBER 5
    float Step[STEP_SIZE_NUMBER] = { LightMarchingRayStep*2.0f, LightMarchingRayStep, LightMarchingRayStep*0.5f, LightMarchingRayStep*0.25f, MarchingRayStep };
    uint StepSizeIndex = 0;
    uint StepIteration = 1;
    float DistCandidate = 0.0f;

    while( true )
    {
        float sampleDist = DistCandidate + (Step[StepSizeIndex] * StepIteration);
        vec3 samplePoint = origin + dir * sampleDist;
        pointDensity = SampleDensity(samplePoint);
        if ( pointDensity <= FLT_EPS )
        {
            DistCandidate = DistCandidate + (Step[StepSizeIndex] * (StepIteration-1));

            StepSizeIndex++;
            if( StepSizeIndex == STEP_SIZE_NUMBER )
            {
                return DistCandidate;
            }
            else
            {
                StepIteration = 1;  // skipping over 0 itteration of the next step size, not to sample same point again
            }
        }
        else
        {
            StepIteration++;
        }

    }
}

float FindSurfaceSlow(vec3 origin, vec3 dir)
{
    float steps = 0.0f;
    float coveredDst = 0.0f;
    float pointDensity = 0.0f;
    do
    {
        steps++;
        vec3 samplePoint = origin + dir * coveredDst;
        pointDensity = SampleDensity(samplePoint);
        
        coveredDst += LightMarchingRayStep;

    } while ( pointDensity > FLT_EPS );
    //return steps;
    return coveredDst - LightMarchingRayStep;
}

float SampleDensity(vec3 location)
{
    vec3 localPoint = (inverseBoundingBox * vec4(location, 1.0)).xyz;
    localPoint = localPoint + vec3(0.5f);
    //ivec3 mapSize = ivec3(DensitiesMapSize);
    vec3 sampleCoord = localPoint * DensitiesMapSize;

    float Density = imageLoad(densitiesMap, ivec3(sampleCoord.xyz)).r;
       
    return max(0.0f, Density - DensityOffset);
}

bool IntersectRayBox(vec3 origin, vec3 dir, out vec3 hitLocation, out float distInsideCube2)
{
    vec3 localOrigin = (inverseBoundingBox * vec4(origin, 1.0)).xyz;
    vec3 localDir = normalize((inverseBoundingBox * vec4(dir, 0.0)).xyz);

    // Box bounds
    vec3 minBound = vec3(-0.5f);
    vec3 maxBound = vec3(0.5f);

    // Calculate the t-values for intersection with each slab
    vec3 tMin = (minBound - localOrigin) / localDir;
    vec3 tMax = (maxBound - localOrigin) / localDir;

    // Ensure tMin < tMax
    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);

    // Find the largest entry t and the smallest exit t
    float tNear = max(max(t1.x, t1.y), t1.z);
    float tFar = min(min(t2.x, t2.y), t2.z);

    // If tNear > tFar or tFar < 0, there is no intersection
    if (tNear > tFar || tFar < FLT_EPS)
    {
        return false;
    }

    // Populate the hit info
    hitLocation = localOrigin + localDir * tNear;
    vec3 hitFar = localOrigin + localDir * tFar;

    hitLocation = (BoundingBox * vec4(hitLocation, 0.0)).xyz;
    hitFar = (BoundingBox * vec4(hitFar, 0.0)).xyz;

    //float scaleToWorld = length((BoundingBox * vec4(ray.dir, 1)).xyz);
    //float nearDistance = scaleToWorld * tFar;
    //float farDistance = scaleToWorld * tFar;

    vec3 offset = (hitFar - hitLocation);

    distInsideCube2 = dot(offset, offset);

    return true;
}