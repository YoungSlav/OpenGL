#version 430

layout(local_size_x = 256) in;

struct Particle
{
    vec2 Position;
    vec2 PredictedPosition;
    vec2 Velocity;

    float Density;
    float NearDensity;
};

// BUFFERS
layout(std430, binding = 0) buffer ParticlesBuffer
{
	Particle Particles[];
};
layout(std430, binding = 5) buffer ParticlesSortedBuffer
{
	Particle SortedParticles[];
};
layout(std430, binding = 2) buffer StartIndicesBuffer
{
	uint StartIndices[];
};

// world params
uniform vec2 WorldSize;
uniform vec2 ContainerSize;
uniform vec2 RandomVector;
uniform int ParticleCount;

// particle params
uniform float ParticleMass;
uniform float ParticleRadius;
uniform float SmoothingRadius;
uniform float iSmoothingRadius;
uniform float TargetDensity;
uniform float SimulationTimeStep;

// force params
uniform float Preassure;
uniform float NearPreassure;
uniform float ViscosityFactor;
uniform float CollisionDamping;

// kernel scales
uniform float DensityScale;
uniform float NearDensityScale;
uniform float PressureScale;
uniform float NearPressureScale;
uniform float ViscosityScale;

// external forces
uniform float GravityForce;
uniform float InteractionForce;
uniform vec2 InteractionLocation;
uniform float InteractionRadius;





// forces
vec2 CalcFluidForces(uint i);

// conversion helpers
float DensityToPressure(float density);
float NearDensityToPressure(float density);

// kernels
float DensityKernel(float dst);
float NearDensityKernel(float dst);
float PressureKernel(float dst);
float NearPressureKernel(float dst);
float ViscosityKernel(float dst);

// grid helpers
uint GetCellIndex(vec2 Position);
ivec2 GetCellCoord(vec2 Position);
uint GetCellHash(ivec2 Coords);
uint CellIndexFromHash(uint Hash);

// extrenal forces
vec2 CalcExternalForces(uint i);
void ResolveCollisions(vec2 OldPosition, inout vec2 Position, inout vec2 Velocity);


const uint GridLookupCount = 9;
const ivec2 GridOffsets[GridLookupCount] = ivec2[](
    ivec2(0, 0),
	ivec2(1, 0),
	ivec2(-1, 0),
	ivec2(0, 1),
	ivec2(0, -1),
	ivec2(1, 1),
	ivec2(1, -1),
	ivec2(-1, 1),
	ivec2(-1, -1)
);

const float PredictionTimeStep = 1.0f / 120.0f;

void main()
{
    uint i = gl_GlobalInvocationID.x;
    if ( i >= ParticleCount ) return;

    vec2 externalForces = CalcExternalForces(i);

    vec2 fluidForces = CalcFluidForces(i);

    vec2 newVelocity = SortedParticles[i].Velocity + ((fluidForces + externalForces) * SimulationTimeStep);
    vec2 newPosition = SortedParticles[i].Position + (newVelocity * SimulationTimeStep);

    ResolveCollisions(SortedParticles[i].Position, newPosition, newVelocity);

    // output 
    Particles[i].Position = newPosition;
    Particles[i].Velocity = newVelocity;
    Particles[i].PredictedPosition = newPosition + newVelocity * PredictionTimeStep;
};

vec2 CalcFluidForces(uint i)
{
    vec2 viscosityForce = vec2(0.0f);
    vec2 pressureForce = vec2(0.0f);
    
    vec2 myVelocity = SortedParticles[i].Velocity;
    float myDensity = SortedParticles[i].Density;
    float myDensityInverse = 1.0f / SortedParticles[i].Density;
    float myNearDensityInverse = 1.0f / SortedParticles[i].NearDensity;
    float myNearDensity = SortedParticles[i].NearDensity;
    float myPressure = DensityToPressure(myDensity);
    float myNearPressure = NearDensityToPressure(myNearDensity);

    vec2 myPosition = SortedParticles[i].PredictedPosition;
    ivec2 myCell = GetCellCoord(myPosition);
    for ( uint gr = 0; gr < GridLookupCount; ++gr )
    {
        ivec2 lookupCell = myCell + GridOffsets[gr];
        uint CellHash = GetCellHash(lookupCell);
        uint CellIndex = CellIndexFromHash(CellHash);
        for ( uint otherP = StartIndices[CellIndex]; otherP < ParticleCount; ++otherP )
        {
            if ( otherP == i ) continue;
            vec2 otherPosition = SortedParticles[otherP].PredictedPosition;
            uint otherCellIndex = GetCellIndex(otherPosition);
            if ( otherCellIndex != CellIndex )
                break;

            vec2 offset = myPosition - otherPosition;
            float dst2 = dot(offset,  offset);
            if ( dst2 > SmoothingRadius*SmoothingRadius ) continue;
            
            float dst = sqrt(dst2);            
            vec2 dir = dst > 0.0f ? offset / dst : RandomVector;
            
            
            float otherDensity = SortedParticles[otherP].Density;
            float otherNearDensity = SortedParticles[otherP].NearDensity;
            float otherDensityInverse = 1.0f / otherDensity;
            float otherNearDensityInverse = 1.0f / otherNearDensity;
            
            float otherPressure = DensityToPressure(otherDensity);
            float otherNearPressure = NearDensityToPressure(otherNearDensity);

            float sharedPressure = (myPressure + otherPressure) * 0.5f;
            float sharedNearPressure = (myNearPressure + otherNearPressure) * 0.5f;
            
            pressureForce += dir * PressureKernel(dst) * sharedPressure * otherDensityInverse;
            pressureForce += dir * NearPressureKernel(dst) * sharedNearPressure * otherNearDensityInverse;
            
            vec2 otherVelocity = SortedParticles[otherP].Velocity;
            viscosityForce += (otherVelocity - myVelocity) * ParticleMass * otherDensityInverse * ViscosityKernel(dst);
        }
    }
    return (viscosityForce * ViscosityFactor) + (pressureForce * myDensityInverse);
}

vec2 CalcExternalForces(uint i)
{
    vec2 externalForces = vec2(0.0f, GravityForce);
    if ( InteractionForce != 0.0f )
    {
        vec2 offset = InteractionLocation - SortedParticles[i].Position;
        float dst2 = dot(offset, offset);
        if ( dst2 < InteractionRadius*InteractionRadius )
        {
            float dst = sqrt(dst2);
            float normilizeDist = 1.0f - (dst / InteractionRadius);
            vec2 forceDirection = offset / dst;
            float gravityWeight = 1 - (normilizeDist * clamp(InteractionForce / 10.0f, 0.0f, 1.0f));
            
            vec2 acceleration =
                externalForces * gravityWeight +
                forceDirection * normilizeDist * InteractionForce -
                SortedParticles[i].Velocity * normilizeDist;

            return acceleration;
        }
    }
    return externalForces;
}

void ResolveCollisions(vec2 OldPosition, inout vec2 Position, inout vec2 Velocity)
{
    vec2 Bounds = ContainerSize - vec2(ParticleRadius);
    vec2 collisionPoint = Position;
    vec2 collisionNormal = vec2(0.0); // Normal vector for collision

    // Check for collisions and compute the collision normal
    if (abs(collisionPoint.x) > Bounds.x)
    {
        collisionPoint.x = Bounds.x * sign(collisionPoint.x);
        collisionNormal.x = -sign(collisionPoint.x); // Normal is perpendicular to the X wall
    }
    if (abs(collisionPoint.y) > Bounds.y)
    {
        collisionPoint.y = Bounds.y * sign(collisionPoint.y);
        collisionNormal.y = -sign(collisionPoint.y); // Normal is perpendicular to the Y wall
    }

    // If a collision happened
    if (collisionNormal != vec2(0.0))
    {
        // Reflect velocity using the collision normal
        Velocity = Velocity - 2.0 * dot(Velocity, collisionNormal) * collisionNormal;
        // Apply damping to the velocity
        Velocity *= CollisionDamping;

        const vec2 originalOffset = (Position - OldPosition);
        const float originalMotion2 = dot(originalOffset, originalOffset);


        const vec2 actualOffset = (collisionPoint - OldPosition);
        const float actualMotion2 = dot(actualOffset, actualOffset);

        const float coveredDt2 = actualMotion2 / originalMotion2;
        const float leftDt = 1.0f - sqrt(coveredDt2);

        Position = collisionPoint + Velocity * SimulationTimeStep * leftDt;
    }
}

//// HELPERS
float PressureKernel(float dst)
{
    float v = SmoothingRadius - dst;
	return v * PressureScale;
}
float NearPressureKernel(float dst)
{
    float v = SmoothingRadius - dst;
	return v * v * NearPressureScale;
}
float ViscosityKernel(float dst)
{
    float v = SmoothingRadius * SmoothingRadius - dst * dst;
	return v * v * v * ViscosityScale;
}
float DensityToPressure(float density)
{
    return (density - TargetDensity) * Preassure;
}
float NearDensityToPressure(float density)
{
    return density * NearPreassure;
}

// GRID

uint GetCellIndex(vec2 Position)
{
    return CellIndexFromHash(GetCellHash(GetCellCoord(Position)));
}


ivec2 GetCellCoord(vec2 Position)
{
	vec2 normilizedPos = Position + WorldSize;
	int cellX = int(floor(normilizedPos.x * iSmoothingRadius*2.0f));
    int cellY = int(floor(normilizedPos.y * iSmoothingRadius*2.0f));
	return ivec2(cellX, cellY);
}

uint GetCellHash(ivec2 Coords)
{	
	const uint PRIME1 = 0x9e3779b1;
    const uint PRIME2 = 0x85ebca6b;

	return Coords.y * PRIME1 + Coords.x * PRIME2;
}

uint CellIndexFromHash(uint Hash)
{
    return Hash % ParticleCount;
}