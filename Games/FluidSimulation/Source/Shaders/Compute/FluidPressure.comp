#version 430

layout(local_size_x = 256) in;

struct Particle
{
    vec2 Position;
    vec2 PredictedPosition;
    vec2 Velocity;
    float Density;
    float iDensity;
};
struct SortedParticle
{
    uint CellIndex;
    uint pIndex;
};

// BUFFERS
layout(std430, binding = 0) buffer ParticlesBuffer
{
	Particle Particles[];
};
layout(std430, binding = 1) buffer SortedParticlesBuffer
{
	SortedParticle SortedParticles[];
};
layout(std430, binding = 2) buffer StartIndicesBuffer
{
	uint StartIndices[];
};

// world params
uniform vec2 WorldSize;
uniform vec2 ContainerSize;
uniform float MaxVelocity;
uniform vec2 RandomVector;
uniform uint ParticleCount;

// particle params
uniform float ParticleMass;
uniform float ParticleRadius;
uniform float SmoothingRadius;
uniform float TargetDensity;
uniform float SimulationTimeStep;

// force params
uniform float Preassure;
uniform float ViscosityFactor;
uniform float CollisionDamping;

// kernel scales
uniform float DensityScale;
uniform float PressureScale;
uniform float ViscosityScale;

// external forces
uniform float GravityForce;
uniform float InteractionForce;
uniform vec2 InteractionLocation;
uniform float InteractionRadius;

const float PredictionTimeStep = 1.0f / 120.0f;



// forces
void CalcDensity(uint i);
vec2 CalcFluidForces(uint i);
void ResolveCollisions(uint i);

// conversion helpers
float DensityToPressure(float density);

// kernels
float DensityKernel(float dst);
float PressureKernel(float dst);
float ViscosityKernel(float dst);

// grid helpers
ivec2 GetCellCoord(vec2 Position);
uint GetCellHash(ivec2 Coords);
uint CellIndexFromHash(uint Hash);

// extrenal forces
vec2 CalcExternalForces(uint i);
void ResolveCollisions(uint i, inout vec2 Position, inout vec2 Velocity);


const uint GridLookupCount = 9;
const ivec2 GridOffsets[GridLookupCount] = ivec2[](
    ivec2(0, 0),
	ivec2(1, 0),
	ivec2(-1, 0),
	ivec2(0, 1),
	ivec2(0, -1),
	ivec2(1, 1),
	ivec2(1, -1),
	ivec2(-1, 1),
	ivec2(-1, -1)
);

void main()
{
    uint i = gl_GlobalInvocationID.x;
    if ( i >= ParticleCount ) return;

    CalcDensity(i);

    barrier();

    vec2 externalForces = CalcExternalForces(i);

    vec2 fluidForces = CalcFluidForces(i);

    vec2 newVelocity = Particles[i].Velocity + (fluidForces + externalForces)* SimulationTimeStep;
    vec2 newPosition = Particles[i].Position + newVelocity * SimulationTimeStep;

    ResolveCollisions(i, newPosition, newVelocity);

    Particles[i].Velocity = newVelocity;
    Particles[i].Position = newPosition;

    Particles[i].PredictedPosition = newPosition + newVelocity * PredictionTimeStep;
};

void CalcDensity(uint i)
{
    float Density = 0.0f;

    vec2 myPosition = Particles[i].PredictedPosition;
    ivec2 myCell = GetCellCoord(myPosition);
    for ( uint gr = 0; gr < GridLookupCount; ++gr )
    {
        ivec2 lookupCell = myCell + GridOffsets[gr];
        uint CellHash = GetCellHash(lookupCell);
        uint CellIndex = CellIndexFromHash(CellHash);
        for ( uint lookupIndex = StartIndices[CellIndex]; lookupIndex < ParticleCount; ++lookupIndex )
        {
            if ( SortedParticles[lookupIndex].CellIndex != CellIndex )
                break;
            uint otherP = SortedParticles[lookupIndex].pIndex;

            // now we got all the indices
            vec2 otherPosition = Particles[otherP].PredictedPosition;
            vec2 offset = myPosition - otherPosition;
            float dst2 = dot(offset, offset);
            if ( dst2 > SmoothingRadius*SmoothingRadius ) continue;
            
            // now we have all the data

            Density += ParticleMass * DensityKernel(dst2);
        }
    }
    Particles[i].Density = Density;
    Particles[i].iDensity = 1.0f / Density;
}

vec2 CalcFluidForces(uint i)
{
    vec2 viscosityForce = vec2(0.0f);
    vec2 pressureForce = vec2(0.0f);
    
    vec2 myVelocity = Particles[i].Velocity;
    float myDensity = Particles[i].Density;
    float myPressure = DensityToPressure(myDensity);

    vec2 myPosition = Particles[i].PredictedPosition;
    ivec2 myCell = GetCellCoord(myPosition);
    for ( uint gr = 0; gr < GridLookupCount; ++gr )
    {
        ivec2 lookupCell = myCell + GridOffsets[gr];
        uint CellHash = GetCellHash(lookupCell);
        uint CellIndex = CellIndexFromHash(CellHash);
        for ( uint lookupIndex = StartIndices[CellIndex]; lookupIndex < ParticleCount; ++lookupIndex )
        {
            if ( SortedParticles[lookupIndex].CellIndex != CellIndex )
                break;
            uint otherP = SortedParticles[lookupIndex].pIndex;
            if ( otherP == i ) continue;

            // now we got all the indices

            vec2 otherPosition = Particles[otherP].PredictedPosition;
            vec2 offset = myPosition - otherPosition;
            float dst2 = dot(offset,  offset);
            if ( dst2 > SmoothingRadius*SmoothingRadius ) continue;
            
            float dst = sqrt(dst2);
            
            // now we have all the data
            
            vec2 dir = dst > 0.0f ? offset / dst : RandomVector;
            
            
            float otherDensity = Particles[otherP].Density;
            vec2 otherVelocity = Particles[otherP].Velocity;
            
            float otherPressure = DensityToPressure(otherDensity);

            float densityFactor = Particles[otherP].iDensity;
            
            pressureForce += ((myPressure + otherPressure) * 0.5f * densityFactor) * dir * PressureKernel(dst);
            
            viscosityForce += ((otherVelocity - myVelocity) * densityFactor) * ViscosityKernel(dst);
        }
    }
    return (viscosityForce * ViscosityFactor) - pressureForce;
}

vec2 CalcExternalForces(uint i)
{
    vec2 externalForces = vec2(0.0f, GravityForce);
    if ( InteractionForce != 0.0f )
    {
        vec2 offset = InteractionLocation - Particles[i].Position;
        float dst = length(offset);
        if ( dst < InteractionRadius )
        {
            float normilizeDist = 1.0f - (dst / InteractionRadius);
            vec2 forceDirection = offset / dst;
            float gravityWeight = 1 - (normilizeDist * clamp(InteractionForce / 10.0f, 0.0f, 1.0f));
            
            vec2 acceleration =
                externalForces * gravityWeight +
                forceDirection * normilizeDist * InteractionForce -
                Particles[i].Velocity * normilizeDist;

            return acceleration;
        }
    }
    return externalForces;
}

void ResolveCollisions(uint i, inout vec2 Position, inout vec2 Velocity)
{
    vec2 Bounds = ContainerSize - vec2(ParticleRadius);
    vec2 myPosition = Position;
    vec2 collisionNormal = vec2(0.0); // Normal vector for collision

    // Check for collisions and compute the collision normal
    if (abs(myPosition.x) > Bounds.x)
    {
        myPosition.x = Bounds.x * sign(myPosition.x);
        collisionNormal.x = -sign(myPosition.x); // Normal is perpendicular to the X wall
    }
    if (abs(myPosition.y) > Bounds.y)
    {
        myPosition.y = Bounds.y * sign(myPosition.y);
        collisionNormal.y = -sign(myPosition.y); // Normal is perpendicular to the Y wall
    }

    // If a collision happened
    if (collisionNormal != vec2(0.0))
    {
        Position = myPosition;

        // Reflect velocity using the collision normal
        Velocity = Velocity - 2.0 * dot(Velocity, collisionNormal) * collisionNormal;

        // Apply damping to the velocity
        Velocity *= CollisionDamping;
    }
}

void ResolveCollisions(uint i)
{
    vec2 Bounds = ContainerSize - vec2(ParticleRadius);
    bool bResult = false;
    vec2 myPosition = Particles[i].Position;
    vec2 VelocityModifyDir = vec2(0.0f);
	if ( abs(myPosition.x) > Bounds.x )
	{
		myPosition.x = Bounds.x * sign(myPosition.x);
		VelocityModifyDir.x = -1;
		bResult = true;
	}
	if ( abs(myPosition.y) > Bounds.y )
	{
		myPosition.y = Bounds.y * sign(myPosition.y);
		VelocityModifyDir.y = -1;
		bResult = true;
	}
    if ( bResult )
    {
        Particles[i].Position = myPosition;
        Particles[i].Velocity *= (VelocityModifyDir * CollisionDamping);
    }
}

//// HELPERS

float DensityKernel(float dst2)
{
    float v = SmoothingRadius*SmoothingRadius - dst2;
	return v * v * v * DensityScale;
}

float PressureKernel(float dst)
{
    float v = SmoothingRadius*SmoothingRadius - dst*dst;
	return v * v * v * PressureScale;
}
float ViscosityKernel(float dst)
{
    float v = SmoothingRadius - dst;
	return v * ViscosityScale;
}
float DensityToPressure(float density)
{
    return max((density - TargetDensity), 0.0f) * Preassure;
}

// GRID

ivec2 GetCellCoord(vec2 Position)
{
	vec2 normilizedPos = Position + WorldSize;
	int cellX = int(floor(normilizedPos.x / SmoothingRadius*2.0f));
    int cellY = int(floor(normilizedPos.y / SmoothingRadius*2.0f));
	return ivec2(cellX, cellY);
}

uint GetCellHash(ivec2 Coords)
{	
	const uint PRIME1 = 0x9e3779b1;
    const uint PRIME2 = 0x85ebca6b;

	return Coords.y * PRIME1 + Coords.x * PRIME2;
}

uint CellIndexFromHash(uint Hash)
{
    return Hash % ParticleCount;
}